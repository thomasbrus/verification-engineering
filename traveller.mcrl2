% CONFIG
% ======

% Aliasses
sort LocationId = Int;
sort CarrierId = Int;
sort CardId = Int;

% Carrier ids
map NS, SYNTUS, TLS : CarrierId;
eqn TLS = 0;
eqn NS = 1;
eqn SYNTUS = 2;

map NUMBER_OF_CARDS : Nat;
eqn NUMBER_OF_CARDS = 3;

% Mapping from carrier ids to location ids (-1 when not checked in)
sort CardStatusMapping = CarrierId -> LocationId;

map default_card_mapping, default_card_values : CardStatusMapping;
eqn default_card_mapping = default_card_values[TLS -> -1][NS -> -1][SYNTUS -> -1];

% Mapping from two location ids two the price of that route
map prices, price_table : LocationId # LocationId -> Real;

var location_x, location_y : LocationId;
eqn location_x == location_y -> prices(location_x, location_y) = 0;
    location_x != location_y -> prices(location_x, location_y) = price_table(min(location_x, location_y), max(location_x,location_y));

eqn price_table(50, 51) = 1;
eqn price_table(51, 52) = 2;
eqn price_table(52, 50) = 3;

% ACTIONS
% =======

act
  show_card, read_card, show: CardId # CarrierId # LocationId; 
  send_card_info, receive_card_info, card_info: CardId # LocationId # LocationId # Real;
  send_card_info_tls, receive_card_info_tls, card_info_tls: CardId # CardStatusMapping # Real;
  write, update, process: CardId # LocationId # Real;
  write_tls, update_tls, process_tls: CardId # CardStatusMapping # Real;
  action_status, reader_status : Bool # Real;

% PROCESSES
% =========

proc
  Card(card_id : Int, card_data : CardStatusMapping, credit : Real) =
    sum carrier_id : CarrierId, location_id : LocationId . (0 <= carrier_id && carrier_id < 3) ->
      show_card(card_id, carrier_id, location_id) . (
        % In case of carrier
        (carrier_id != TLS) ->
          send_card_info(card_id, card_data(carrier_id), card_data(TLS), credit) . (
            sum updated_location_id : LocationId, updated_credit : Real .
              update(card_id, updated_location_id, updated_credit) .
              sum received_status : Bool . action_status(received_status, updated_credit) .
              Card(card_id, card_data[carrier_id -> updated_location_id][TLS -> -1], updated_credit)
          ) +

        % In case of TLS
        (carrier_id == TLS) ->
          send_card_info_tls(card_id, card_data, credit) . (
            sum updated_card_status : CardStatusMapping, updated_credit : Real .
              update_tls(card_id, updated_card_status, updated_credit) .
              sum received_status : Bool . action_status(received_status, updated_credit) .
              Card(card_id, updated_card_status, updated_credit)
          )
      );

  CarrierReader(carrier_id : CarrierId, location_id : LocationId) = 
    sum card_id : CardId . (100 <= card_id && card_id < 100 + NUMBER_OF_CARDS) ->
      read_card(card_id, carrier_id, location_id) .
      sum received_location_id : LocationId, received_location_id_tls : LocationId, received_credit : Real .
        receive_card_info(card_id, received_location_id, received_location_id_tls, received_credit) . (
        ( % Check out at carrier, charge correct fee
          (received_location_id != -1) ->
            ( write(card_id, -1, received_credit - prices(location_id, received_location_id)) .
              action_status(true, received_credit - prices(location_id, received_location_id))
            ) +

          % Check out at TLS, charge correct fee
          (received_location_id == -1 && received_location_id_tls != -1) ->
            ( write(card_id, -1, received_credit - prices(location_id, received_location_id_tls)) .
              action_status(true, received_credit - prices(location_id, received_location_id_tls))
            ) +

          % Check in at carrier
          (received_location_id == -1 && received_location_id_tls == -1 && received_credit > 0) ->
            (write(card_id, location_id, received_credit) . action_status(true, received_credit)) +

          % Insufficient credit
          (received_location_id == -1 && received_location_id_tls == -1 && received_credit <= 0) ->
            write(card_id, -1, received_credit) . action_status(false, received_credit)
        ) .
        CarrierReader(carrier_id, location_id)
      );

  OvercheckReader(location_id : LocationId) = 
    sum received_card_id : CardId . (100 <= received_card_id && received_card_id < 100 + NUMBER_OF_CARDS) ->
      read_card(received_card_id, TLS, location_id) .
      sum received_card_status : CardStatusMapping, received_credit : Real .
      receive_card_info_tls(received_card_id, received_card_status, received_credit) . (
        ( % Check out at NS, check in at TLS
          (received_card_status(NS) != -1 && received_card_status(SYNTUS) == -1 && received_card_status(TLS) == -1) ->
            ( write_tls(received_card_id,
                        received_card_status[NS-> -1][TLS-> location_id],
                        received_credit - prices(received_card_status(NS), location_id)) .

              action_status(true, received_credit - prices(received_card_status(NS), location_id))
            ) +

          % Check out at Syntus, check in at TLS
          (received_card_status(SYNTUS) != -1 && received_card_status(NS) == -1 && received_card_status(TLS) == -1) ->
            ( write_tls(received_card_id,
                        received_card_status[SYNTUS-> -1][TLS -> location_id],
                        received_credit - prices(received_card_status(SYNTUS), location_id)) .

              action_status(true, received_credit - prices(received_card_status(SYNTUS), location_id))
            ) +

          % Checked in at two carriers, refuse to overcheck
          (received_card_status(SYNTUS) != -1 && received_card_status(NS) != -1) ->
            ( write_tls(received_card_id, received_card_status, received_credit) .
              action_status(false, received_credit)
            ) +
        
          % Charge previous route, update TLS check-in
          (received_card_status(TLS) != -1 && received_card_status(TLS) != location_id) ->
            ( write_tls(received_card_id,
                        received_card_status[TLS -> location_id],
                        received_credit - prices(received_card_status(TLS), location_id)) .

              action_status(true, received_credit - prices(received_card_status(TLS), location_id))
            ) +

          % Trying to check over twice
          (received_card_status(TLS) != -1 && received_card_status(TLS) == location_id) ->
            ( write_tls(received_card_id, received_card_status, received_credit) .
              action_status(false, received_credit)
            ) + 
          
          % Not checked in
          (received_card_status(TLS) == -1 && received_card_status(NS) == -1 && received_card_status(SYNTUS) == -1) ->
            ( write_tls(received_card_id, received_card_status, received_credit) .
              action_status(false, received_credit)
            )
        ) .         
        OvercheckReader(location_id) 
      );

  System =
    Card(100, default_card_mapping, 5) ||
    % Card(101, default_card_mapping, 5) ||
    % Card(102, default_card_mapping, 5) ||
    CarrierReader(SYNTUS, 50) ||
    CarrierReader(SYNTUS, 51) ||
    CarrierReader(NS, 51) ||
    CarrierReader(NS, 52) ||
    OvercheckReader(51);

% INITIALIZATION
% ==============

init
  hide({ card_info, process, card_info_tls, process_tls },

    allow({ show, card_info, process, card_info_tls, process_tls, reader_status },
    
      comm({ action_status | action_status -> reader_status,
             send_card_info | receive_card_info -> card_info, 
             show_card | read_card -> show,
             write | update -> process, 
             write_tls | update_tls -> process_tls,
             send_card_info_tls | receive_card_info_tls -> card_info_tls }, System)));

